/*
 * Copyright 2020-present, Nike, Inc.
 * All rights reserved.
 * 
 * This source code is licensed under the Apache-2.0 license found in
 * the LICENSE file in the root directory of this source tree.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'com.jfrog.bintray' version "1.8.5"
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

//http://www.gradle.org/docs/current/userguide/java_plugin.html
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {

    api 'com.beust:jcommander:1.72'
    api 'org.springframework:spring-context:5.2.11.RELEASE'
    api 'org.slf4j:slf4j-api:1.7.30'

    implementation 'com.github.tomas-langer:chalk:1.0.2'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.reflections:reflections:0.9.12'

    testImplementation "junit:junit:4.13.1"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

jar {
    doFirst {
        ant.replace(file: "$buildDir/resources/main/gimme-a-cli.properties", token: "@@RELEASE@@", value: archiveVersion)
    }
}

javadoc {
    failOnError = false
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

apply from: 'gradle/publishing.gradle'
apply from: 'gradle/bintray.gradle'